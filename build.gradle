plugins {
    id 'application'
    id "org.barfuin.gradle.taskinfo" version "1.3.1"
}

application {
    mainClass = 'com.gradlehero.themepark.RideStatusService'
}

tasks.register('runJar', JavaExec) {
    classpath tasks.named('jar').map {it.outputs }
    classpath configurations.runtimeClasspath
    args 'teacups'
    mainClass = 'com.gradlehero.themepark.RideStatusService'
}

tasks.named('jar') {
    manifest {
        attributes('Main-Class': 'com.gradlehero.themepark.RideStatusService')
    }
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

repositories {
    mavenCentral()
}

tasks.withType(Test).configureEach() {
    it.useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach() {
    options.verbose = true
}

tasks.named('processResources') {
    include '**/*.txt'
}

tasks.named('jar') {
    archiveFileName = 'test.jar'
}